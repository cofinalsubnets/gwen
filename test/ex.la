(:
 ; church numerals
 ; named for alonzo church, inventor of lambda notation.
 ; dedekind (1888 ¶126) uses a similar idea when describing
 ; how natural numbers give rise to monoids of endomorphisms.
 ((zero) -) -
 one (zero zero)
 (((up op i) g) f) ((f (op g)) i)
 (((succ g) x) y) ((g x) (x y))
 (((ps f) g) h) (h (g f))
 (((pred f) g) h) (((f (ps g)) (\ h)) one)
 ((add g) f) ((f succ) g)
 mul (up add zero)
 pow (up mul one)
 tet (up pow one)
 pen (up tet one)

 (C n) (? (= n 0) zero (succ (C (- n 1)))) ; ℕ->⛪
 (N c) ((c (\ x (+ x 1))) 0)               ; ⛪->ℕ

 two (succ one) four (two two) five (succ four)
 three (succ two)
 ten ((mul two) five)
 one-hundred (two ten)

 ; fixnum hyperoperation sequence, rooted at +
 (hy x n y) ( ? (~ n) (+ x y) (~ y) 1
   (hy x (- n 1) (hy x n (- y 1))))

 ; cantor's pairing function reversibly encodes a
 ; pair of natural numbers as a natural number.
 (c1895 m n)
  (+ m (/ (* (+ m n) (+ m n 1)) 2))
 ; according to stillwell (2018) this is the only known
 ; quadratic pairing function & was proved in 1923 by
 ; polya & fueter to be (with its image under flip) the only
 ; quadratic pairing function sending (0, 0) to 0.
 ; this reverses it:
 (c1895~ p) (:
  (f t n) (? (> (+ t n) p)
           (X t (- n 1))
           (f (+ t n) (+ n 1)))
  x (f 0 1)
  m (- p (A x))
  n (- (B x) m)
  (L m n)))

; some miscellaneous functions
(test functions

 (: 0to23 (iota 24)
    (check p) (= p (c1895~ (ap c1895 p)))
    pairs (flat-map 0to23 (\ n (map (\ m (L m n)) 0to23)))
  (all check pairs))

 (: (ack m n)
   (? (< m 1) (+ n 1)
    (ack (- m 1) (? (> n 0) (ack m (- n 1)) 1)))
  (= 1021 (ack 3 7)
   ; ack(m > 0, n) = 2[m](n + 3) - 3
   (- (hy 2 2 10) 3)
   (- (N ((pow two) ten)) 3)))
 
 (: (fib n)
   (? (< n 3) 1
    (+ (fib (- n 1)) (fib (- n 2))))
  (= 144 (fib 12)))
 
 (: quine ((\ q (L q (Q q))) '(\ q (L q (Q q))))
  (= quine (ev quine)))
 (: (tarai x y z) (?
     (<= x y) y
     (tarai (tarai (- x 1) y z)
            (tarai (- y 1) x z)
            (tarai (- z 1) x y)))
  (= 13 (tarai 12 13 14)))

 (= 100
  (N one-hundred)
  (+ 1 (N (pred one-hundred))))
 (= 252
  (N ((add (five three)) (two three)))
  (N ((mul (two three))
      ((mul (two two))
       (pred (pred (two three)))))))
    

 ; hyperoperations
 (: (pow a b) (hy a 2 b)
    (tet a b) (hy a 3 b)
  (= 65536 (<< 1 16) (pow 2 16) (tet 2 4))))
